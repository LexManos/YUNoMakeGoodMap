buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
plugins {
	id 'com.matthewprenger.cursegradle' version '1.0.7'
}

repositories {
    mavenLocal()
    flatDir {
        name "fileRepo"
        dirs "repo"
    }
}
configurations { deployJars }

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

version = "1.12-6.3.0"
group = "net.minecraftforge.lex"
archivesBaseName = "YUNoMakeGoodMap"

ext.build_number = "${System.getenv().BUILD_NUMBER}"
logger.lifecycle "Build: " + build_number
if (build_number.toString().equals('null')) build_number = '0'
logger.lifecycle "Build: " + build_number

version += '.' + build_number

minecraft {
    version = "1.12-14.21.1.2395"
    runDir = 'run'
    mappings = "snapshot_20170703"

    replace "@MOD_VERSION@", project.version
    replaceIn "YUNoMakeGoodMap.java"
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar
{
    manifest
    {
        attributes 'FMLAT': 'YUNoMakeGoodMap_at.cfg'
    }
}

curseforge {
	apiKey = project.hasProperty('curseforge_apikey') ? project.curseforge_apikey : '0'
	project {
		id = '252104'
		//changelog = file('changelog.html')
		//changelogType = 'html'
		releaseType = 'beta'
	}
}

afterEvaluate {
	//tasks.curseforge238222.dependsOn.add(makeChangelog)
}

uploadArchives {
    dependsOn build // This is important it is needed to actually cause reobf to run!
    repositories {
        add getProject().repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        configuration = configurations.deployJars

        if (project.hasProperty('forgeMavenPassword')) {
            repository(url: "http://files.minecraftforge.net/maven/manage/upload") {
                authentication(userName: project.getProperty('forgeMavenUser'), password: project.getProperty('forgeMavenPassword'))
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'Simple void world mod for Minecraft.'
            url 'https://github.com/LexManos/YUNoMakeGoodMap'

            scm {
                url 'https://github.com/LexManos/YUNoMakeGoodMap'
                connection 'scm:git:git://github.com/LexManos/YUNoMakeGoodMape.git'
                developerConnection 'scm:git:git@github.com:LexManos/YUNoMakeGoodMap.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/LexManos/YUNoMakeGoodMap/issues'
            }

            licenses {
                license {
                    name 'Apache License version 2.0'
                    url 'https://github.com/LexManos/YUNoMakeGoodMap/blob/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'LexManos'
                    name 'Lex Manos'
                    roles { role 'developer' }
                }
            }
        }
    }
}
